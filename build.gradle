plugins {
	id 'java'
	id 'groovy'
	id 'idea'
	id 'org.springframework.boot' version '2.7.9'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

idea {
    module {
        excludeDirs += files("node_modules")
    }
}

group = 'ro.scriptorium'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '19'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation group: 'com.google.guava', name: 'guava', version: '20.0'

	implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.10.5'
	implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.10.5'
	implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
	implementation group: 'org.modelmapper', name: 'modelmapper', version: '2.4.4'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}


// the regular app
task runAppIonicBuild {
    final String dir = "src/main/webapp-ionic"
    final subpath="app"
    doLast {
        exec {
            workingDir "./$dir"
            executable 'npm'
            args 'install'
        }
        exec {
            workingDir "./$dir"
            executable 'ionic'
            args 'build', "--prod",  "--", "--base-href=/$subpath/"
        }
        copy {
            from "./$dir/www/"
            into "$buildDir/resources/main/static/$subpath"
        }
//        copy {
//            from "./$dir/www/index.html"
//            into "$buildDir/resources/main/static/app/"
//            filter { line -> line.replaceAll('href="/"', 'href="/app/"') }
//        }
    }
}

task ionic  {
    dependsOn runAppIonicBuild
}

//tasks.processResources.dependsOn << runAdminIonicBuild
tasks.bootRun.dependsOn << ionic
tasks.jar.dependsOn << ionic
tasks.bootJar.dependsOn << ionic